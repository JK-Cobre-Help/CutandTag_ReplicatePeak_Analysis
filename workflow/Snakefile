configfile: "config/config.yml"

# Author: Kevin Boyd
# Date: Dec 3, 2024
# Adapted scripts from: https://github.com/SansamLab-Pipelines-Genomics/ReplicatePeakAnalyzer

##################################################################
##                    Define input functions                    ##
##################################################################

import pandas as pd

# Read the CSV file and set an index using the values in the "sample" column.
samples_table = pd.read_csv(config["samples_csv"]).set_index("sample", drop=False)

# Expand function to extract BAM paths
bam_paths = expand("{bam}", bam=samples_table["bam"].tolist())

# Filter samples by their set
def filter_sample_by_set(Set, samples_table):
    """
    Filter the samples_table to include only the samples
    belonging to a given 'Set' and return their names.
    """
    filtered_rows = samples_table[samples_table["set"] == Set]
    return list(set(filtered_rows["sample"].tolist()))

unique_marks = list(dict.fromkeys(samples_table["mark"]))
unique_conditions = list(dict.fromkeys(samples_table["condition"]))

def sets_for_mark_condition(mark, condition, df):
    """
    Return a list of unique 'set' values where (mark, condition) match.
    """
    subset = df[(df["mark"] == mark) & (df["condition"] == condition)]
    return list(set(subset["set"]))


##################################################################
##                          Rule All                            ##
##################################################################

rule all:
    input:
        # Ensure BAM and BigWig files for consensus peaks are created
        expand("results/macs2Peaks/{sample}_0.05_peaks.narrowPeak", sample=samples_table.index),
        expand("results/mergedPeaks/{Set}_merged_peaks.rds", Set=list(set(samples_table["set"]))),
        expand("results/consensusPeaks/{Set}_consensus_peaks.bed", Set=list(set(samples_table["set"]))),
        expand("results/consensusPeaks/{Set}_consensus_peaks.bam", Set=list(set(samples_table["set"]))),
        expand("results/consensusPeaks/{Set}_consensus_peaks.bw", Set=list(set(samples_table["set"]))),
        expand("results/eulerPlot/{Set}_eulerPlot.pdf", Set=list(set(samples_table["set"]))),
        "results/overlaps/MidpointOverlaps.bed",
        "results/heatmap/Beds_All_Samples.gz",
        "results/heatmap/HeatPlots_All_Samples.png",
        expand("results/eulerPlotMarkByCondition/{mark}_{condition}_eulerPlot.pdf",
               mark=unique_marks, condition=unique_conditions)

##################################################################
##                        MACS2 Peak Calling                    ##
##################################################################

rule call_peaks_with_macs2:
    input:
        bam=lambda wildcards: samples_table.loc[wildcards.sample, "bam"]
    output:
        peaks="results/macs2Peaks/{sample}_0.05_peaks.narrowPeak",
        summary="results/macs2Peaks/{sample}_macs2Peak_summary.txt"
    params:
        genome=config["genome"],
        qvalue=config["macs2_qvalue"],
        sample_name="{sample}",
        output_dir="results/macs2Peaks/"
    envmodules:
        config["macs2"]
    shell:
        """
        macs2 callpeak -t {input.bam} \
        -g {params.genome} -f BAMPE -n {params.sample_name}_{params.qvalue} \
        --outdir {params.output_dir} -q {params.qvalue} --keep-dup all --nomodel \
        2>{params.output_dir}/{params.sample_name}_macs2Peak_summary.txt
        """

##################################################################
##                     Generate Merged Peaks                    ##
##################################################################

rule generate_merged_peaks:
    input:
        lambda wildcards: expand(
            "results/macs2Peaks/{sample}_0.05_peaks.narrowPeak",
            sample=filter_sample_by_set(wildcards.Set, samples_table)
        )
    output:
        merged="results/mergedPeaks/{Set}_merged_peaks.rds"
    envmodules:
        config["R"],
        config["bioconductor"]
    shell:
        """
        Rscript scripts/generateMergedPeaks.R {input} {output.merged}
        """

##################################################################
##                Make Bed of Reproducible Peaks                ##
##################################################################

rule make_bed_of_reproducible_peaks:
    input:
        merged="results/mergedPeaks/{Set}_merged_peaks.rds"
    output:
        consensus="results/consensusPeaks/{Set}_consensus_peaks.bed"
    params:
        min_overlap=config["minNumberOfSampleOverlaps"]
    envmodules:
        config["R"],
        config["bioconductor"]
    shell:
        """
        Rscript scripts/generateConsensusPeaks.R {input.merged} {params.min_overlap} {output.consensus}
        """

##################################################################
##                 Create BAM from Consensus Peaks              ##
##################################################################

rule create_bam_from_consensus:
    input:
        bed="results/consensusPeaks/{Set}_consensus_peaks.bed"
    output:
        bam="results/consensusPeaks/{Set}_consensus_peaks.bam",
        sorted_bed="results/consensusPeaks/{Set}_consensus_peaks_sorted.bed"
    params:
        genome_file=config["chrom_sizes"]
    envmodules:
        config["bedtools"],
        config["samtools"]
    shell:
        """
        # Ensure the BED file is sorted
        bedtools sort -i {input.bed} > {output.sorted_bed}

        # Convert the sorted BED file to BAM and index it
        bedtools bedtobam -i {output.sorted_bed} -g {params.genome_file} > {output.bam}
        samtools index {output.bam}
        """

##################################################################
##               Create BigWig from Consensus Peaks             ##
##################################################################

rule create_bigwig_from_consensus:
    input:
        bam="results/consensusPeaks/{Set}_consensus_peaks.bam"
    output:
        bigwig="results/consensusPeaks/{Set}_consensus_peaks.bw"
    params:
        bin_size=config["binSize"],
        genome_size=config["effective_genome_size"]
    envmodules:
        config["deeptools"]
    shell:
        """
        bamCoverage --bam {input.bam} --outFileName {output.bigwig} \
        --binSize {params.bin_size} --effectiveGenomeSize {params.genome_size} --normalizeUsing CPM
        """

##################################################################
##                  Make Euler with Merged Peaks                ##
##################################################################

rule make_euler_plot_of_overlaps_with_merged:
    input:
        merged_rds="results/mergedPeaks/{Set}_merged_peaks.rds"
    output:
        euler_rds="results/eulerPlot/{Set}_eulerPlot.rds",
        euler_pdf="results/eulerPlot/{Set}_eulerPlot.pdf"
    params:
        euler_fontsize=config["EulerFontSize"],
        euler_colors=config["EulerColors"],
        pdf_width=config["EulerWidth"],
        pdf_height=config["EulerHeight"]
    envmodules:
        config["R"],
        config["bioconductor"]
    shell:
        """
        Rscript scripts/makeEulerOfOverlappingPeaks.R {input.merged_rds} {output.euler_rds} {output.euler_pdf} \
        {params.euler_fontsize} "{params.euler_colors}" {params.pdf_width} {params.pdf_height}
        """

##################################################################
##                   Midpoint and Peak Overlaps                 ##
##################################################################

rule midpoint_and_peak_overlaps:
    input:
        # Collect reproducible peaks BED files (already sorted by earlier rules)
        reproducible_beds=expand("results/consensusPeaks/{Set}_consensus_peaks.bed", Set=list(set(samples_table["set"])))
    output:
        # Outputs: midpoint and full overlaps
        midpoint="results/overlaps/MidpointOverlaps.bed",
        peak="results/overlaps/PeakOverlaps.bed"
    params:
        out_dir="results/overlaps/"
    envmodules:
        config["R"],
        config["bioconductor"]
    shell:
        """
        Rscript scripts/midpointAndPeakOverlaps.R {input.reproducible_beds} {params.out_dir}
        """

##################################################################
##                Generate Heatmap for All Samples              ##
##################################################################

# Convert your CSV column into a list in CSV order
all_sets = samples_table["set"]  # e.g., ['H3K27ac_C', 'H3K27ac_T', 'H3K27ac_C', ...]

# Retain only unique sets in the first-seen order
unique_sets_in_order = list(dict.fromkeys(all_sets))

rule generate_heatmap:
    input:
        # Input bigWig files (all consensus peaks bigwigs)
        bigwigs = expand("results/consensusPeaks/{Set}_consensus_peaks.bw", Set=unique_sets_in_order),
        # Input MidpointOverlaps BED file
        midpoints="results/overlaps/MidpointOverlaps.bed"
    output:
        # Output matrix and heatmap
        matrix="results/heatmap/Beds_All_Samples.gz",
        heatmap="results/heatmap/HeatPlots_All_Samples.png"
    params:
        # Extend regions before and after the midpoint
        before_region=3000,
        after_region=3000,
        processors=8,
        refPointName="Center",
        regionsName="Midpoints",
        sampleOrder=config["sampleOrder"]
    envmodules:
        config["deeptools"]
    shell:
        """
        echo "Using sampleOrder: {params.sampleOrder}"

        computeMatrix reference-point \
            -S {input.bigwigs} \
            -R {input.midpoints} \
            --outFileName {output.matrix} \
            -a {params.after_region} -b {params.before_region} \
            --numberOfProcessors {params.processors} \
            --smartLabels

        plotHeatmap \
            -m {output.matrix} \
            -out {output.heatmap} \
            --dpi 1000 \
            --sortUsing sum \
            --sortUsingSamples "{params.sampleOrder}" \
            --refPointLabel "{params.refPointName}" \
            --regionsLabel "{params.regionsName}"
        """

##################################################################
##                Euler Plot by Mark and Condition              ##
##################################################################

rule euler_plot_by_mark_and_condition:
    """
    Generate an Euler plot of consensus .bed overlaps for each combination
    of mark and condition. For example: (H3K27ac, C) or (H3K27ac, T), etc.
    """
    # We will expand across all unique marks and conditions
    input:
        # We gather all the consensus beds associated with that mark+condition
        lambda wc: [
            f"results/consensusPeaks/{s}_consensus_peaks.bed"
            for s in sets_for_mark_condition(wc.mark, wc.condition, samples_table)
        ]
    output:
        rds = "results/eulerPlotMarkByCondition/{mark}_{condition}_eulerPlot.rds",
        pdf = "results/eulerPlotMarkByCondition/{mark}_{condition}_eulerPlot.pdf"
    params:
        # Assign some default eulerr arguments or read them from config
        font_size = config.get("EulerFontSize", 12),
        colors = ",".join(config.get("EulerColors", ["#E41A1C","#377EB8","#4DAF4A"])),
        pdf_width = config.get("EulerWidth", 7),
        pdf_height = config.get("EulerHeight", 5)
    shell:
        """
        # Turn the list of input bed files into comma-separated strings
        BED_FILES=$(echo {input} | tr ' ' ',')
        
        # For labeling each set, use the final subdirectory name (or however you prefer):
        SET_NAMES=$(echo {input} | sed 's/results\\/consensusPeaks\\///g' | sed 's/_consensus_peaks.bed//g' | tr ' ' ',')
        
        Rscript scripts/makeEulerPlotOfBedOverlaps.R \
          "{BED_FILES}" \
          "{SET_NAMES}" \
          {output.rds} \
          {output.pdf} \
          {params.font_size} \
          "{params.colors}" \
          {params.pdf_width} \
          {params.pdf_height}
        """
